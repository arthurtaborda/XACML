namespace com.glispa {	
	
	policy publisherPolicy {
		target clause Attributes.subjectIdQualifier=="publisher" and Attributes.actionId=="run"
		apply firstApplicable
		
		/*
		 * Inactive publishers can not run any campaign
		 */
		rule denyIfPublisherIsInactive {
			deny
			condition publisher.status=="Inactive"
		}
		
		rule allowWhitelistedCategories {
			condition stringAtLeastOneMemberOf(campaign.categoryId, publisher.whitelistedCategories)
			permit
		}
		
		rule allowWhitelistedOperationSystems {
			condition stringAtLeastOneMemberOf(campaign.os, publisher.whitelistedOperationalSystems)
			permit
		}
	}
	
	policy campaignPolicy {
		target clause resource.resourceType=="campaign" and Attributes.actionId=="run"
		apply denyOverrides
		
		/*
		 * Inactive campaigns can not be run by any publisher
		 */
		rule denyIfCampaignIsInactive {
			condition campaign.active==false
			deny
		}
	}
	
	namespace campaign {		
		attribute categoryId {
	        id = "category:id"
	        type = string
	        category = resourceCat
	    }	
		attribute os {
	        id = "category:os"
	        type = string
	        category = resourceCat
	    }
		attribute active {
	        id = "category:active"
	        type = boolean
	        category = resourceCat
	    }
	}
	namespace publisher {
		attribute status {
	        id = "publisher:status"
	        type = string
	        category = subjectCat
	    }
		attribute whitelistedCategories {
	        id = "publisher:categories"
	        type = string
	        category = subjectCat
	    }
		attribute whitelistedOperationalSystems {
	        id = "publisher:operational-systems"
	        type = string
	        category = subjectCat
	    }
	}
	namespace resource {
		attribute resourceType {
			id = "resource:type"
			type = string
			category = resourceCat
		}
	}
}
